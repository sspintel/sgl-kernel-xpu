# Setup building flags for SYCL device and host codes.

# Import device IP version detection utility.
include(${CMAKE_CURRENT_LIST_DIR}/DeviceDetection.cmake)

function(CHECK_SYCL_FLAG FLAG VARIABLE_NAME)
  set(TEMP_DIR "${CMAKE_BINARY_DIR}/temp")
  file(MAKE_DIRECTORY ${TEMP_DIR})
  set(TEST_SRC_FILE "${TEMP_DIR}/check_options.cpp")
  set(TEST_EXE_FILE "${TEMP_DIR}/check_options.out")
  file(WRITE ${TEST_SRC_FILE} "#include <iostream>\nint main() { std::cout << \"Checking compiler options ...\" << std::endl; return 0; }\n")
  execute_process(
      COMMAND ${SYCL_COMPILER} -fsycl -ftemplate-backtrace-limit=0 ${TEST_SRC_FILE} -o ${TEST_EXE_FILE} ${FLAG}
      WORKING_DIRECTORY ${TEMP_DIR}
      OUTPUT_VARIABLE output
      ERROR_VARIABLE output
      RESULT_VARIABLE result
      TIMEOUT 60
  )
  if(result EQUAL 0)
      set(${VARIABLE_NAME} TRUE PARENT_SCOPE)
  else()
      set(${VARIABLE_NAME} FALSE PARENT_SCOPE)
  endif()
  file(REMOVE_RECURSE ${TEMP_DIR})
endfunction()

# Support GCC on Linux.
if(CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
  set(SYCL_HOST_FLAGS)
  set(SYCL_KERNEL_OPTIONS)
  set(SYCL_COMPILE_FLAGS ${SYCL_FLAGS})
  set(SYCL_DEVICE_LINK_FLAGS ${SYCL_LINK_FLAGS})
  set(SYCL_OFFLINE_COMPILER_AOT_OPTIONS)
  set(SYCL_OFFLINE_COMPILER_CG_OPTIONS)
  set(SYCL_OFFLINE_COMPILER_FLAGS)

  # # -- Host flags (SYCL_CXX_FLAGS)
  list(APPEND SYCL_HOST_FLAGS -fPIC)
  list(APPEND SYCL_HOST_FLAGS -std=c++20)
  # SYCL headers warnings
  list(APPEND SYCL_HOST_FLAGS -Wno-deprecated-declarations)
  list(APPEND SYCL_HOST_FLAGS -Wno-deprecated)
  list(APPEND SYCL_HOST_FLAGS -Wno-attributes)
  list(APPEND SYCL_HOST_FLAGS -Wno-sign-compare)

  if(CMAKE_BUILD_TYPE MATCHES Debug)
    list(APPEND SYCL_HOST_FLAGS -g -fno-omit-frame-pointer -O0)
  elseif(CMAKE_BUILD_TYPE MATCHES RelWithDebInfo)
    list(APPEND SYCL_HOST_FLAGS -g -O2)
  endif()
  if(USE_PER_OPERATOR_HEADERS)
    list(APPEND SYCL_HOST_FLAGS -DAT_PER_OPERATOR_HEADERS)
  endif()
  list(APPEND SYCL_HOST_FLAGS -D__INTEL_LLVM_COMPILER_VERSION=${__INTEL_LLVM_COMPILER})
  # -- Kernel flags (SYCL_KERNEL_OPTIONS)
  # The fast-math will be enabled by default in SYCL compiler.
  # Refer to [https://clang.llvm.org/docs/UsersManual.html#cmdoption-fno-fast-math]
  # 1. We enable below flags here to be warn about NaN and Infinity,
  # which will be hidden by fast-math by default.
  # 2. The associative-math in fast-math allows floating point
  # operations to be reassociated, which will lead to non-deterministic
  # results compared with CUDA backend.
  # 3. The approx-func allows certain math function calls (such as log, sqrt, pow, etc)
  # to be replaced with an approximately equivalent set of instructions or
  # alternative math function calls, which have great errors.
  #
  # PSEUDO of separate compilation with DPCPP compiler.
  # 1. Kernel source compilation:
  # icpx -fsycl -fsycl-target=${SYCL_TARGETS_OPTION} ${SYCL_FLAGS} -fsycl-host-compiler=gcc -fsycl-host-compiler-options='${CMAKE_HOST_FLAGS}' kernel.cpp -o kernel.o
  # 2. Device code linkage:
  # icpx -fsycl -fsycl-target=${SYCL_TARGETS_OPTION} -fsycl-link ${SYCL_DEVICE_LINK_FLAGS} -Xs '${SYCL_OFFLINE_COMPILER_FLAGS}' kernel.o -o device-code.o
  # 3. Host only source compilation:
  # gcc ${CMAKE_HOST_FLAGS} host.cpp -o host.o
  # 4. Linkage:
  # gcc -shared host.o kernel.o device-code.o -o libxxx.so

  set(SYCL_KERNEL_OPTIONS ${SYCL_KERNEL_OPTIONS} -ftemplate-backtrace-limit=0)
  set(SYCL_KERNEL_OPTIONS ${SYCL_KERNEL_OPTIONS} -fno-sycl-unnamed-lambda)
  set(SYCL_KERNEL_OPTIONS ${SYCL_KERNEL_OPTIONS} -sycl-std=2020)
  set(SYCL_KERNEL_OPTIONS ${SYCL_KERNEL_OPTIONS} -fhonor-nans)
  set(SYCL_KERNEL_OPTIONS ${SYCL_KERNEL_OPTIONS} -fhonor-infinities)
  set(SYCL_KERNEL_OPTIONS ${SYCL_KERNEL_OPTIONS} -fno-associative-math)
  set(SYCL_KERNEL_OPTIONS ${SYCL_KERNEL_OPTIONS} -fno-approx-func)
  set(SYCL_KERNEL_OPTIONS ${SYCL_KERNEL_OPTIONS} -Wno-absolute-value)
  set(SYCL_KERNEL_OPTIONS ${SYCL_KERNEL_OPTIONS} -no-ftz)
  set(SYCL_KERNEL_OPTIONS ${SYCL_KERNEL_OPTIONS} -fno-sycl-instrument-device-code)

  if(CMAKE_BUILD_TYPE MATCHES Debug)
    set(SYCL_KERNEL_OPTIONS ${SYCL_KERNEL_OPTIONS} -g -O0 -Rno-debug-disables-optimization)
  elseif(CMAKE_BUILD_TYPE MATCHES RelWithDebInfo)
    set(SYCL_KERNEL_OPTIONS ${SYCL_KERNEL_OPTIONS} -gline-tables-only -O2)
  endif()

  set(SYCL_KERNEL_OPTIONS ${SYCL_KERNEL_OPTIONS} -D__INTEL_LLVM_COMPILER_VERSION=${__INTEL_LLVM_COMPILER})

  CHECK_SYCL_FLAG("-fsycl-fp64-conv-emu" SUPPORTS_FP64_CONV_EMU)
  if(SUPPORTS_FP64_CONV_EMU)
    set(SYCL_KERNEL_OPTIONS ${SYCL_KERNEL_OPTIONS} -fsycl-fp64-conv-emu)
  else()
    message(WARNING "The compiler does not support the '-fsycl-fp64-conv-emu' flag, \
    will disable it. On some platforms that don't support FP64, \
    running operations with the FP64 datatype will raise a Runtime error: Required aspect fp64 is not supported on the device \
    or a Native API failed error.")
  endif()

  set(TORCH_XPU_OPS_FLAGS ${SYCL_HOST_FLAGS})

  # -- SYCL device object linkage flags
  include(ProcessorCount)
  ProcessorCount(proc_cnt)
  if((DEFINED ENV{MAX_JOBS}) AND ("$ENV{MAX_JOBS}" LESS_EQUAL ${proc_cnt}))
    set(SYCL_MAX_PARALLEL_LINK_JOBS $ENV{MAX_JOBS})
  else()
    set(SYCL_MAX_PARALLEL_LINK_JOBS ${proc_cnt})
  endif()
  set(SYCL_DEVICE_LINK_FLAGS ${SYCL_DEVICE_LINK_FLAGS} -fsycl-max-parallel-link-jobs=${SYCL_MAX_PARALLEL_LINK_JOBS})
  set(SYCL_DEVICE_LINK_FLAGS ${SYCL_DEVICE_LINK_FLAGS} --offload-compress)

  set(SYCL_OFFLINE_COMPILER_CG_OPTIONS "${SYCL_OFFLINE_COMPILER_CG_OPTIONS} -options -cl-poison-unsupported-fp64-kernels")
  set(SYCL_OFFLINE_COMPILER_CG_OPTIONS "${SYCL_OFFLINE_COMPILER_CG_OPTIONS} -options -cl-intel-enable-auto-large-GRF-mode")
  set(SYCL_OFFLINE_COMPILER_CG_OPTIONS "${SYCL_OFFLINE_COMPILER_CG_OPTIONS} -options -cl-fp32-correctly-rounded-divide-sqrt")
  set(SYCL_OFFLINE_COMPILER_CG_OPTIONS "${SYCL_OFFLINE_COMPILER_CG_OPTIONS} -options -cl-intel-greater-than-4GB-buffer-required")

  set(AOT_TARGETS)

  # Resolve DPCPP_SYCL_TARGET: user-provided takes priority, otherwise auto-detect
  if(DPCPP_SYCL_TARGET)
    message(STATUS "Using user-provided DPCPP_SYCL_TARGET: ${DPCPP_SYCL_TARGET}")
  else()
    get_device_ip_version(DEVICE_IP_VERSION)
    message(STATUS "Detected device IP version: ${DEVICE_IP_VERSION}")
    if(DEVICE_IP_VERSION EQUAL 20)
      set(DPCPP_SYCL_TARGET "bmg")
    elseif(DEVICE_IP_VERSION EQUAL 35)
      set(DPCPP_SYCL_TARGET "intel_gpu_cri")
    elseif(DEVICE_IP_VERSION EQUAL 40)
      set(DPCPP_SYCL_TARGET "intel_gpu_jgs")
    else()
      message(WARNING "Unknown device IP version: ${DEVICE_IP_VERSION}. Cannot auto-detect target.")
    endif()
  endif()

  message(STATUS "DPCPP_SYCL_TARGET set to: ${DPCPP_SYCL_TARGET}")

  # Map DPCPP_SYCL_TARGET to AOT_TARGETS and compile definitions
  if(DPCPP_SYCL_TARGET MATCHES "bmg")
    list(APPEND AOT_TARGETS "bmg_g21")
  elseif(DPCPP_SYCL_TARGET MATCHES "cri")
    list(APPEND AOT_TARGETS "cri")
    add_compile_definitions(SGL_PRE_SILICON)
  elseif(DPCPP_SYCL_TARGET MATCHES "jgs")
    list(APPEND AOT_TARGETS "xe4")
    add_compile_definitions(SGL_PRE_SILICON)
  else()
    message(WARNING "Unknown DPCPP_SYCL_TARGET: ${DPCPP_SYCL_TARGET}. No AOT target set.")
  endif()

  list(REMOVE_DUPLICATES AOT_TARGETS)
  string(JOIN "," AOT_TARGETS_STR ${AOT_TARGETS})
  set(SYCL_KERNEL_OPTIONS ${SYCL_KERNEL_OPTIONS} ${SYCL_TARGETS_OPTION})
  set(SYCL_DEVICE_LINK_FLAGS ${SYCL_DEVICE_LINK_FLAGS} ${SYCL_TARGETS_OPTION})
  set(SYCL_OFFLINE_COMPILER_AOT_OPTIONS "-device ${AOT_TARGETS_STR}")
  message(STATUS "Compile Intel GPU AOT Targets for ${AOT_TARGETS}")

  set(SYCL_COMPILE_FLAGS ${SYCL_COMPILE_FLAGS} ${SYCL_KERNEL_OPTIONS})

  set(SYCL_OFFLINE_COMPILER_FLAGS "${SYCL_OFFLINE_COMPILER_AOT_OPTIONS}${SYCL_OFFLINE_COMPILER_CG_OPTIONS}")
else()
  message("Not compiling with XPU. Currently only support GCC compiler on Linux as CXX compiler.")
  return()
endif()
